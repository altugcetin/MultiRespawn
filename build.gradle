plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

group = 'net.astroalchemist'
version = '1.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'dev.folia:folia-api:1.21.4-R0.1-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'redis.clients:jedis:5.1.0'
    implementation 'com.mysql:mysql-connector-j:8.3.0'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName.set('MultiRespawn')
    archiveClassifier.set('')
    archiveVersion.set(version)
    relocate 'com.zaxxer.hikari', 'net.astroalchemist.multirespawn.libs.hikari'
    relocate 'redis.clients', 'net.astroalchemist.multirespawn.libs.redis'
}

build.dependsOn shadowJar
